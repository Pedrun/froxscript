repeat = { repeat_literal? ~ assignment ~ comment? ~ EOI }
    repeat_literal = @{ integer ~ "#" }
    comment = { ";" ~ ANY* }

assignment = { (attribute_literal ~ assigner)? ~ expression }
    assigner = _{ assign_eq | assign_add | assign_sub | assign_mul | assign_div }
        assign_eq = { "=" }
        assign_add = { "+=" }
        assign_sub = { "-=" }
        assign_mul = { "*=" | "\\*=" }
        assign_div = { "/=" }

expression = { prefix* ~ primary ~ postfix* ~ (infix ~ prefix* ~ primary ~ postfix* )* }

prefix = _{ neg | not | ceil | round | floor }
    neg = { "-" }
    not = { "!" }
    ceil = { "^" }
    round = { "~" }
    floor = { "_" }

postfix = _{ percent }
    percent = { "%" }

infix = _{ hyper_add | hyper_sub | add | sub | div | mul | counter_less | counter_greater | less_eq | less | greater_eq | greater | eq | or | and }
    hyper_add = { "++" }
    hyper_sub = { "--" }
    add = { "+" }
    sub = { "-" }
    mul = { "*" | "\\*" }
    div = { "/" }
    counter_less = { "<<" }
    counter_greater = { ">>" }
    less_eq = { "<=" }
    less = { "<" }
    greater_eq = { ">=" }
    greater = { ">" }
    eq = { "==" }
    or = { "|" }
    and = { "&" }

primary = _{ fate_dice | dice | frac | number | group }
    fate_dice = { dice_n ~ "df" }
    dice = ${ dice_n ~ "d" ~ dice_side ~ roll_config }
        dice_n = { number? }
        dice_side = { number }
        roll_config = ${ explode? ~ keep_drop? ~ aro? ~ sort? }
            explode = ${ "!" ~ number? }
            keep_drop = ${ (keep_low | keep_high | drop_high | drop_low | crit) ~ number }
                keep_high = { "kh" | "k" }
                keep_low = { "kl" }
                drop_high = { "dh" }
                drop_low = { "dl" | "d" }
                crit = { "c" }
            aro = { "aro" }
            sort = { "s" }
    frac = @{ '0'..'9'* ~ "." ~ '0'..'9'+ }
    number = _{ attribute | integer }
        attribute = @{ integer? ~ attribute_literal }
        attribute_literal = { "$"? ~ ('A'..'Z' | "_")+ }
        integer = @{ '0'..'9'+ }
    group = { "(" ~ expression ~ ")" }


WHITESPACE = _{ " " | "\t" }